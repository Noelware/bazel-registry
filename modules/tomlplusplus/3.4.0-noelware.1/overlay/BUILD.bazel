load("@bazel_skylib//rules:common_settings.bzl", "bool_flag")
load("@rules_cc//cc:defs.bzl", "cc_library")

_clang_flags = [
    "-ferror-limit=5",
    "-fmax-errors=5",

    # unnecessary pedantry:
    "-Wno-unused-command-line-argument",
    "-Wno-reserved-macro-identifier",
    "-Wno-c++98-compat",
    "-Wno-c++98-compat-pedantic",
    "-Wno-documentation",
    "-Wno-documentation-unknown-command",
    "-Wno-covered-switch-default",
]

_gcc_flags = [
    "-fchar8_t",
    "-std=c++17",
    "-Wno-switch-enum",
    "-Wno-padded",
    "-Wno-float-equal",
]

_clang_pedantic_flags = [
    "-Weverything",
]

_gcc_pedantic_flags = [
    "-Wno-init-list-lifetime",
    "-Wcast-align",
    "-Wcast-qual",
    "-Wctor-dtor-privacy",
    "-Wdisabled-optimization",
    "-Wfloat-equal",
    "-Wimport",
    "-Winit-self",
    "-Wlogical-op",
    "-Wmissing-declarations",
    "-Wmissing-field-initializers",
    "-Wmissing-format-attribute",
    "-Wmissing-include-dirs",
    "-Wmissing-noreturn",
    "-Wold-style-cast",
    "-Woverloaded-virtual",
    "-Wpacked",
    "-Wpointer-arith",
    "-Wredundant-decls",
    "-Wshadow",
    "-Wsign-conversion",
    "-Wsign-promo",
    "-Wstack-protector",
    "-Wstrict-null-sentinel",
    "-Wswitch-default",
    "-Wswitch-enum",
    "-Wundef",
    "-Wunreachable-code",
    "-Wunused",
    "-Wunused-parameter",
    "-Wuseless-cast",
    "-Wvariadic-macros",
    "-Wwrite-strings",
    "-Wmissing-noreturn",
]

_msvc_flags = [
    "/std:c++17",
    "/bigobj",
    "/Gy",  # function-level linking
    "/GF",  # string pooling
    "/openmp-",
    "/permissive-",
    "/utf-8",
    "/volatile:iso",
    "/Zc:__cplusplus",
    "/Zc:inline",
    "/Zc:externConstexpr",
    "/Zc:preprorcessor",
]

_bool_flags = [
    ## --@toml++//:pedantic=[True|False] (default: False)
    ("pedantic", False),

    ## --@toml++//:exceptions=[True|False] (default: True)
    ("exceptions", True),

    ## --@toml++//:unreleased_features=[True|False] (default: False)
    ("unreleased_features", False),
]

[bool_flag(
    name = flag,
    build_setting_default = default,
    visibility = ["//visibility:public"],
) for (flag, default) in _bool_flags]

[config_setting(
    name = "%s_enabled" % flag,
    flag_values = {
        (":%s" % flag): "True",
    },
    visibility = [":__pkg__"],
) for (flag, _) in _bool_flags]

[config_setting(
    name = "%s_%s" % (compiler, flag),
    flag_values = {
        (":%s" % flag): "True",
        "@rules_cc//cc/compiler": compiler,
    },
    visibility = [":__pkg__"],
) for (flag, _) in _bool_flags for compiler in [
    "clang",
    "gcc",
    "clang-cl",
    "msvc-cl",
]]

cc_library(
    name = "toml++",
    srcs = ["src/toml.cpp"] + glob([
        "include/toml++/impl/*.hpp",
        "include/toml++/impl/*.inl",
    ]) + glob([
        "vendor/*.hpp",
    ]),
    hdrs = [
        "include/toml++/toml.h",
        "include/toml++/toml.hpp",
    ],
    copts = select({
        "@rules_cc//cc/compiler:clang": _gcc_flags + _clang_flags,
        "@rules_cc//cc/compiler:clang-cl": _gcc_flags + _clang_flags,
        "@rules_cc//cc/compiler:gcc": _gcc_flags,
        "@rules_cc//cc/compiler:msvc-cl": _msvc_flags,
        "//conditions:default": [],
    }) + select({
        ":clang-cl_pedantic": _clang_pedantic_flags,
        ":clang_pedantic": _clang_pedantic_flags,
        ":gcc_pedantic": _gcc_pedantic_flags,
        "//conditions:default": [],
    }) + select({
        ":clang-cl_exceptions": ["-fexceptions"],
        ":clang_exceptions": ["-fexceptions"],
        ":gcc_exceptions": ["-fexceptions"],
        ":msvc-cl_exceptions": ["/Zc:throwingNew"],
        "//conditions:default": [],
    }),
    defines = select({
        ":clang-cl_exceptions": ["_HAS_EXCEPTIONS=1"],
        ":clang_exceptions": ["_HAS_EXCEPTIONS=1"],
        ":gcc_exceptions": ["_HAS_EXCEPTIONS=1"],
        ":msvc-cl_exceptions": ["-D_HAS_EXCEPTIONS=1"],
        "//conditions:default": ["_HAS_EXCEPTIONS=0"],
    }),
    includes = ["include/toml++"],
    strip_include_prefix = "include",
    visibility = ["//visibility:public"],
)

alias(
    name = "tomlplusplus",
    actual = "toml++",
    visibility = ["//visibility:public"],
)
